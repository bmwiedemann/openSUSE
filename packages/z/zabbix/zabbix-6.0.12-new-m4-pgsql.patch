Index: configure.ac
===================================================================
--- configure.ac.orig	2022-12-05 09:13:12.947206126 +0100
+++ configure.ac	2023-01-20 20:21:30.286134050 +0100
@@ -1453,25 +1453,15 @@ if test "x$server" = "xyes" || test "x$p
 	fi
 
 	dnl Checking for PostgreSQL support
-	AX_LIB_POSTGRESQL("9.2")
+	AX_LIB_POSTGRESQL([9.2])
 	if test "x$want_postgresql" = "xyes"; then
 		if test "x$have_db" != "xno"; then
 			AC_MSG_ERROR([You can configure for only one database.])
 		fi
 
 		if test "x$found_postgresql" = "xyes"; then
-			if test "$postgresql_version_check" != "1"; then
-				AC_MSG_ERROR([PostgreSQL version mismatch])
-			fi
-
-			have_db="PostgreSQL"
-
-			if test "$postgresql_version_number" -ge 8002000; then
-				have_multirow_insert="yes"
-			fi
-
-		else
-			AC_MSG_ERROR([PostgreSQL library not found])
+	        	have_db="PostgreSQL"
+			have_multirow_insert="yes" # set by default as we are forcing to use min 8.2 postgresql
 		fi
 	fi
 
@@ -1527,7 +1517,7 @@ if test "x$server" = "xyes" || test "x$p
 		AC_MSG_RESULT(no)
 	fi
 
-	DB_CFLAGS="$MYSQL_CFLAGS $ORACLE_CPPFLAGS $POSTGRESQL_CFLAGS $SQLITE3_CPPFLAGS"
+	DB_CFLAGS="$MYSQL_CFLAGS $ORACLE_CPPFLAGS $POSTGRESQL_CPPFLAGS $SQLITE3_CPPFLAGS"
 	DB_LDFLAGS="$MYSQL_LDFLAGS $ORACLE_LDFLAGS $POSTGRESQL_LDFLAGS $SQLITE3_LDFLAGS"
 	DB_LIBS="$MYSQL_LIBS $ORACLE_LIBS $POSTGRESQL_LIBS $SQLITE3_LIBS"
 
Index: m4/ax_compare_version.m4
===================================================================
--- /dev/null	1970-01-01 00:00:00.000000000 +0000
+++ m4/ax_compare_version.m4	2023-01-20 20:20:44.581933215 +0100
@@ -0,0 +1,177 @@
+# ===========================================================================
+#    https://www.gnu.org/software/autoconf-archive/ax_compare_version.html
+# ===========================================================================
+#
+# SYNOPSIS
+#
+#   AX_COMPARE_VERSION(VERSION_A, OP, VERSION_B, [ACTION-IF-TRUE], [ACTION-IF-FALSE])
+#
+# DESCRIPTION
+#
+#   This macro compares two version strings. Due to the various number of
+#   minor-version numbers that can exist, and the fact that string
+#   comparisons are not compatible with numeric comparisons, this is not
+#   necessarily trivial to do in a autoconf script. This macro makes doing
+#   these comparisons easy.
+#
+#   The six basic comparisons are available, as well as checking equality
+#   limited to a certain number of minor-version levels.
+#
+#   The operator OP determines what type of comparison to do, and can be one
+#   of:
+#
+#    eq  - equal (test A == B)
+#    ne  - not equal (test A != B)
+#    le  - less than or equal (test A <= B)
+#    ge  - greater than or equal (test A >= B)
+#    lt  - less than (test A < B)
+#    gt  - greater than (test A > B)
+#
+#   Additionally, the eq and ne operator can have a number after it to limit
+#   the test to that number of minor versions.
+#
+#    eq0 - equal up to the length of the shorter version
+#    ne0 - not equal up to the length of the shorter version
+#    eqN - equal up to N sub-version levels
+#    neN - not equal up to N sub-version levels
+#
+#   When the condition is true, shell commands ACTION-IF-TRUE are run,
+#   otherwise shell commands ACTION-IF-FALSE are run. The environment
+#   variable 'ax_compare_version' is always set to either 'true' or 'false'
+#   as well.
+#
+#   Examples:
+#
+#     AX_COMPARE_VERSION([3.15.7],[lt],[3.15.8])
+#     AX_COMPARE_VERSION([3.15],[lt],[3.15.8])
+#
+#   would both be true.
+#
+#     AX_COMPARE_VERSION([3.15.7],[eq],[3.15.8])
+#     AX_COMPARE_VERSION([3.15],[gt],[3.15.8])
+#
+#   would both be false.
+#
+#     AX_COMPARE_VERSION([3.15.7],[eq2],[3.15.8])
+#
+#   would be true because it is only comparing two minor versions.
+#
+#     AX_COMPARE_VERSION([3.15.7],[eq0],[3.15])
+#
+#   would be true because it is only comparing the lesser number of minor
+#   versions of the two values.
+#
+#   Note: The characters that separate the version numbers do not matter. An
+#   empty string is the same as version 0. OP is evaluated by autoconf, not
+#   configure, so must be a string, not a variable.
+#
+#   The author would like to acknowledge Guido Draheim whose advice about
+#   the m4_case and m4_ifvaln functions make this macro only include the
+#   portions necessary to perform the specific comparison specified by the
+#   OP argument in the final configure script.
+#
+# LICENSE
+#
+#   Copyright (c) 2008 Tim Toolan <toolan@ele.uri.edu>
+#
+#   Copying and distribution of this file, with or without modification, are
+#   permitted in any medium without royalty provided the copyright notice
+#   and this notice are preserved. This file is offered as-is, without any
+#   warranty.
+
+#serial 13
+
+dnl #########################################################################
+AC_DEFUN([AX_COMPARE_VERSION], [
+  AC_REQUIRE([AC_PROG_AWK])
+
+  # Used to indicate true or false condition
+  ax_compare_version=false
+
+  # Convert the two version strings to be compared into a format that
+  # allows a simple string comparison.  The end result is that a version
+  # string of the form 1.12.5-r617 will be converted to the form
+  # 0001001200050617.  In other words, each number is zero padded to four
+  # digits, and non digits are removed.
+  AS_VAR_PUSHDEF([A],[ax_compare_version_A])
+  A=`echo "$1" | sed -e 's/\([[0-9]]*\)/Z\1Z/g' \
+                     -e 's/Z\([[0-9]]\)Z/Z0\1Z/g' \
+                     -e 's/Z\([[0-9]][[0-9]]\)Z/Z0\1Z/g' \
+                     -e 's/Z\([[0-9]][[0-9]][[0-9]]\)Z/Z0\1Z/g' \
+                     -e 's/[[^0-9]]//g'`
+
+  AS_VAR_PUSHDEF([B],[ax_compare_version_B])
+  B=`echo "$3" | sed -e 's/\([[0-9]]*\)/Z\1Z/g' \
+                     -e 's/Z\([[0-9]]\)Z/Z0\1Z/g' \
+                     -e 's/Z\([[0-9]][[0-9]]\)Z/Z0\1Z/g' \
+                     -e 's/Z\([[0-9]][[0-9]][[0-9]]\)Z/Z0\1Z/g' \
+                     -e 's/[[^0-9]]//g'`
+
+  dnl # In the case of le, ge, lt, and gt, the strings are sorted as necessary
+  dnl # then the first line is used to determine if the condition is true.
+  dnl # The sed right after the echo is to remove any indented white space.
+  m4_case(m4_tolower($2),
+  [lt],[
+    ax_compare_version=`echo "x$A
+x$B" | sed 's/^ *//' | sort -r | sed "s/x${A}/false/;s/x${B}/true/;1q"`
+  ],
+  [gt],[
+    ax_compare_version=`echo "x$A
+x$B" | sed 's/^ *//' | sort | sed "s/x${A}/false/;s/x${B}/true/;1q"`
+  ],
+  [le],[
+    ax_compare_version=`echo "x$A
+x$B" | sed 's/^ *//' | sort | sed "s/x${A}/true/;s/x${B}/false/;1q"`
+  ],
+  [ge],[
+    ax_compare_version=`echo "x$A
+x$B" | sed 's/^ *//' | sort -r | sed "s/x${A}/true/;s/x${B}/false/;1q"`
+  ],[
+    dnl Split the operator from the subversion count if present.
+    m4_bmatch(m4_substr($2,2),
+    [0],[
+      # A count of zero means use the length of the shorter version.
+      # Determine the number of characters in A and B.
+      ax_compare_version_len_A=`echo "$A" | $AWK '{print(length)}'`
+      ax_compare_version_len_B=`echo "$B" | $AWK '{print(length)}'`
+
+      # Set A to no more than B's length and B to no more than A's length.
+      A=`echo "$A" | sed "s/\(.\{$ax_compare_version_len_B\}\).*/\1/"`
+      B=`echo "$B" | sed "s/\(.\{$ax_compare_version_len_A\}\).*/\1/"`
+    ],
+    [[0-9]+],[
+      # A count greater than zero means use only that many subversions
+      A=`echo "$A" | sed "s/\(\([[0-9]]\{4\}\)\{m4_substr($2,2)\}\).*/\1/"`
+      B=`echo "$B" | sed "s/\(\([[0-9]]\{4\}\)\{m4_substr($2,2)\}\).*/\1/"`
+    ],
+    [.+],[
+      AC_WARNING(
+        [invalid OP numeric parameter: $2])
+    ],[])
+
+    # Pad zeros at end of numbers to make same length.
+    ax_compare_version_tmp_A="$A`echo $B | sed 's/./0/g'`"
+    B="$B`echo $A | sed 's/./0/g'`"
+    A="$ax_compare_version_tmp_A"
+
+    # Check for equality or inequality as necessary.
+    m4_case(m4_tolower(m4_substr($2,0,2)),
+    [eq],[
+      test "x$A" = "x$B" && ax_compare_version=true
+    ],
+    [ne],[
+      test "x$A" != "x$B" && ax_compare_version=true
+    ],[
+      AC_WARNING([invalid OP parameter: $2])
+    ])
+  ])
+
+  AS_VAR_POPDEF([A])dnl
+  AS_VAR_POPDEF([B])dnl
+
+  dnl # Execute ACTION-IF-TRUE / ACTION-IF-FALSE.
+  if test "$ax_compare_version" = "true" ; then
+    m4_ifvaln([$4],[$4],[:])dnl
+    m4_ifvaln([$5],[else $5])dnl
+  fi
+]) dnl AX_COMPARE_VERSION
Index: m4/ax_lib_postgresql.m4
===================================================================
--- m4/ax_lib_postgresql.m4.orig	2022-12-01 08:47:13.068351647 +0100
+++ m4/ax_lib_postgresql.m4	2023-01-20 20:20:44.581933215 +0100
@@ -1,31 +1,33 @@
-##### http://autoconf-archive.cryp.to/ax_lib_postgresql.html
+# ===========================================================================
+#    https://www.gnu.org/software/autoconf-archive/ax_lib_postgresql.html
+# ===========================================================================
 #
 # SYNOPSIS
 #
-#   AX_LIB_POSTGRESQL([MINIMUM-VERSION])
+#   AX_LIB_POSTGRESQL([MINIMUM-VERSION],[ACTION-IF-FOUND],[ACTION-IF-NOT-FOUND])
 #
 # DESCRIPTION
 #
-#   This macro provides tests of availability of PostgreSQL 'libpq'
-#   library of particular version or newer.
+#   This macro provides tests of availability of PostgreSQL 'libpq' library
+#   of particular version or newer.
 #
-#   AX_LIB_POSTGRESQL macro takes only one argument which is optional.
-#   If there is no required version passed, then macro does not run
-#   version test.
+#   AX_LIB_POSTGRESQL macro takes only one argument which is optional. If
+#   there is no required version passed, then macro does not run version
+#   test.
 #
 #   The --with-postgresql option takes one of three possible values:
 #
 #   no - do not check for PostgreSQL client library
 #
-#   yes - do check for PostgreSQL library in standard locations
-#   (pg_config should be in the PATH)
+#   yes - do check for PostgreSQL library in standard locations (pg_config
+#   should be in the PATH)
 #
-#   path - complete path to pg_config utility, use this option if
-#   pg_config can't be found in the PATH
+#   path - complete path to pg_config utility, use this option if pg_config
+#   can't be found in the PATH (You could set also PG_CONFIG variable)
 #
 #   This macro calls:
 #
-#     AC_SUBST(POSTGRESQL_CFLAGS)
+#     AC_SUBST(POSTGRESQL_CPPFLAGS)
 #     AC_SUBST(POSTGRESQL_LDFLAGS)
 #     AC_SUBST(POSTGRESQL_LIBS)
 #     AC_SUBST(POSTGRESQL_VERSION)
@@ -34,151 +36,212 @@
 #
 #     HAVE_POSTGRESQL
 #
-# LAST MODIFICATION
+#   It execute if found ACTION-IF-FOUND (empty by default) and
+#   ACTION-IF-NOT-FOUND (AC_MSG_FAILURE by default) if not found.
 #
-#   2017-09-26   modified version detection to detect PostgreSQL 10 (Zabbix)
-#   2006-07-16
+# LICENSE
 #
-# COPYLEFT
-#
-#   Copyright (c) 2006 Mateusz Loskot <mateusz@loskot.net>
-#
-#   Copying and distribution of this file, with or without
-#   modification, are permitted in any medium without royalty provided
-#   the copyright notice and this notice are preserved.
+#   Copyright (c) 2008 Mateusz Loskot <mateusz@loskot.net>
+#   Copyright (c) 2014 Sree Harsha Totakura <sreeharsha@totakura.in>
+#   Copyright (c) 2018 Bastien Roucaries <rouca@debian.org>
+#
+#   Copying and distribution of this file, with or without modification, are
+#   permitted in any medium without royalty provided the copyright notice
+#   and this notice are preserved. This file is offered as-is, without any
+#   warranty.
+
+#serial 22
+
+AC_DEFUN([_AX_LIB_POSTGRESQL_OLD],[
+	found_postgresql="no"
+	_AX_LIB_POSTGRESQL_OLD_fail="no"
+	while true; do
+	  AC_CACHE_CHECK([for the pg_config program], [ac_cv_path_PG_CONFIG],
+	    [AC_PATH_PROGS_FEATURE_CHECK([PG_CONFIG], [pg_config],
+	      [[ac_cv_path_PG_CONFIG="";$ac_path_PG_CONFIG --includedir > /dev/null \
+		&& ac_cv_path_PG_CONFIG=$ac_path_PG_CONFIG ac_path_PG_CONFIG_found=:]],
+	      [ac_cv_path_PG_CONFIG=""])])
+	  PG_CONFIG=$ac_cv_path_PG_CONFIG
+	  AS_IF([test "X$PG_CONFIG" = "X"],[break])
+
+	  AC_CACHE_CHECK([for the PostgreSQL libraries CPPFLAGS],[ac_cv_POSTGRESQL_CPPFLAGS],
+		       [ac_cv_POSTGRESQL_CPPFLAGS="-I`$PG_CONFIG --includedir`" || _AX_LIB_POSTGRESQL_OLD_fail=yes])
+	  AS_IF([test "X$_AX_LIB_POSTGRESQL_OLD_fail" = "Xyes"],[break])
+	  POSTGRESQL_CPPFLAGS="$ac_cv_POSTGRESQL_CPPFLAGS"
+
+	  AC_CACHE_CHECK([for the PostgreSQL libraries LDFLAGS],[ac_cv_POSTGRESQL_LDFLAGS],
+		       [ac_cv_POSTGRESQL_LDFLAGS="-L`$PG_CONFIG --libdir`" || _AX_LIB_POSTGRESQL_OLD_fail=yes])
+	  AS_IF([test "X$_AX_LIB_POSTGRESQL_OLD_fail" = "Xyes"],[break])
+	  POSTGRESQL_LDFLAGS="$ac_cv_POSTGRESQL_LDFLAGS"
+
+	  AC_CACHE_CHECK([for the PostgreSQL libraries LIBS],[ac_cv_POSTGRESQL_LIBS],
+		       [ac_cv_POSTGRESQL_LIBS="-lpq"])
+	  POSTGRESQL_LIBS="$ac_cv_POSTGRESQL_LIBS"
+
+	  AC_CACHE_CHECK([for the PostgreSQL version],[ac_cv_POSTGRESQL_VERSION],
+		       [
+			ac_cv_POSTGRESQL_VERSION=`$PG_CONFIG --version | sed "s/^PostgreSQL[[[:space:]]][[[:space:]]]*\([[0-9.]][[0-9.]]*\).*/\1/"` \
+			      || _AX_LIB_POSTGRESQL_OLD_fail=yes
+		       ])
+	  AS_IF([test "X$_AX_LIB_POSTGRESQL_OLD_fail" = "Xyes"],[break])
+	  POSTGRESQL_VERSION="$ac_cv_POSTGRESQL_VERSION"
+
+
+	  dnl
+	  dnl Check if required version of PostgreSQL is available
+	  dnl
+	  AS_IF([test X"$postgresql_version_req" != "X"],[
+	     AC_MSG_CHECKING([if PostgreSQL version $POSTGRESQL_VERSION is >= $postgresql_version_req])
+	     AX_COMPARE_VERSION([$POSTGRESQL_VERSION],[ge],[$postgresql_version_req],
+				[found_postgresql_req_version=yes],[found_postgresql_req_version=no])
+	     AC_MSG_RESULT([$found_postgresql_req_version])
+	  ])
+	  AS_IF([test "Xfound_postgresql_req_version" = "Xno"],[break])
+
+	  found_postgresql="yes"
+	  break
+	done
+])
 
-AC_DEFUN([AX_LIB_POSTGRESQL],
+AC_DEFUN([_AX_LIB_POSTGRESQL_PKG_CONFIG],
 [
-    PG_CONFIG="no"
-
-    AC_ARG_WITH([postgresql],
-        AC_HELP_STRING([--with-postgresql@<:@=ARG@:>@],
-            [use PostgreSQL library @<:@default=no@:>@, optionally specify path to pg_config]
-        ),
-        [
-        if test "x$withval" = "xno"; then
-            want_postgresql="no"
-        elif test "x$withval" = "xyes"; then
-            want_postgresql="yes"
-        else
-            want_postgresql="yes"
-            PG_CONFIG="$withval"
-        fi
-        ],
-        [want_postgresql="no"]
-    )
+  AC_REQUIRE([PKG_PROG_PKG_CONFIG])
+  found_postgresql=no
 
-    POSTGRESQL_CFLAGS=""
-    POSTGRESQL_LDFLAGS=""
-    POSTGRESQL_LIBS=""
-    POSTGRESQL_VERSION=""
+  while true; do
+    PKG_PROG_PKG_CONFIG
+    AS_IF([test X$PKG_CONFIG = X],[break])
+
+    _AX_LIB_POSTGRESQL_PKG_CONFIG_fail=no;
+    AS_IF([test "X$postgresql_version_req" = "X"],
+	  [PKG_CHECK_EXISTS([libpq],[found_postgresql_pkg_config=yes],[found_postgresql=no])],
+	  [PKG_CHECK_EXISTS([libpq >= "$postgresql_version_req"],
+			   [found_postgresql=yes],[found_postgresql=no])])
+    AS_IF([test "X$found_postgresql" = "no"],[break])
+
+    AC_CACHE_CHECK([for the PostgreSQL libraries CPPFLAGS],[ac_cv_POSTGRESQL_CPPFLAGS],
+		   [ac_cv_POSTGRESQL_CPPFLAGS="`$PKG_CONFIG libpq --cflags-only-I`" || _AX_LIB_POSTGRESQL_PKG_CONFIG_fail=yes])
+    AS_IF([test "X$_AX_LIB_POSTGRESQL_PKG_CONFIG_fail" = "Xyes"],[break])
+    POSTGRESQL_CPPFLAGS="$ac_cv_POSTGRESQL_CPPFLAGS"
+
+
+    AC_CACHE_CHECK([for the PostgreSQL libraries LDFLAGS],[ac_cv_POSTGRESQL_LDFLAGS],
+		   [ac_cv_POSTGRESQL_LDFLAGS="`$PKG_CONFIG libpq --libs-only-L --libs-only-other`" || _AX_LIB_POSTGRESQL_PKG_CONFIG_fail=yes])
+    AS_IF([test "X$_AX_LIB_POSTGRESQL_PKG_CONFIG_fail" = "Xyes"],[break])
+    POSTGRESQL_LDFLAGS="$ac_cv_POSTGRESQL_LDFLAGS"
+
+
+    AC_CACHE_CHECK([for the PostgreSQL libraries LIBS],[ac_cv_POSTGRESQL_LIBS],
+		   [ac_cv_POSTGRESQL_LIBS="`$PKG_CONFIG libpq --libs-only-l`" || _AX_LIB_POSTGRESQL_PKG_CONFIG_fail=ye])
+    AS_IF([test "X$_AX_LIB_POSTGRESQL_PKG_CONFIG_fail" = "Xyes"],[break])
+    POSTGRESQL_LIBS="$ac_cv_POSTGRESQL_LIBS"
+
+    dnl already checked by exist but need to be recovered
+    AC_CACHE_CHECK([for the PostgreSQL version],[ac_cv_POSTGRESQL_VERSION],
+		   [ac_cv_POSTGRESQL_VERSION="`$PKG_CONFIG libpq --modversion`" || _AX_LIB_POSTGRESQL_PKG_CONFIG_fail=yes])
+    AS_IF([test "X$_AX_LIB_POSTGRESQL_PKG_CONFIG_fail" = "Xyes"],[break])
+    POSTGRESQL_VERSION="$ac_cv_POSTGRESQL_VERSION"
+
+    found_postgresql=yes
+    break;
+  done
 
-    dnl
-    dnl Check PostgreSQL libraries (libpq)
-    dnl
+])
 
-    if test "x$want_postgresql" = "xyes"; then
-        AC_PATH_PROG([PG_CONFIG], [pg_config], [])
 
-        if test -x "$PG_CONFIG"; then
-            POSTGRESQL_CFLAGS="`$PG_CONFIG --includedir`"
-            if test -n "$POSTGRESQL_CFLAGS"; then
-                POSTGRESQL_CFLAGS="-I$POSTGRESQL_CFLAGS";
-            fi
-            POSTGRESQL_LDFLAGS="`$PG_CONFIG --libdir`"
-            if test -n "$POSTGRESQL_LDFLAGS"; then
-                POSTGRESQL_LDFLAGS="-L$POSTGRESQL_LDFLAGS";
-            fi
-            POSTGRESQL_LIBS="-lpq"
-
-            _save_postgresql_cflags="${CFLAGS}"
-            _save_postgresql_ldflags="${LDFLAGS}"
-            _save_postgresql_libs="${LIBS}"
-            CFLAGS="${CFLAGS} ${POSTGRESQL_CFLAGS}"
-            LDFLAGS="${LDFLAGS} ${POSTGRESQL_LDFLAGS}"
-            LIBS="${LIBS} ${POSTGRESQL_LIBS}"
 
-            AC_MSG_CHECKING([for PostgreSQL libraries])
-            AC_TRY_LINK(
-[
-#include <libpq-fe.h>
-],
+AC_DEFUN([AX_LIB_POSTGRESQL],
 [
-PGconn	*conn;
-conn = PQsetdbLogin(NULL, NULL, NULL, NULL, NULL, NULL, NULL);
-],
-            AC_DEFINE([HAVE_POSTGRESQL], [1], [Define to 1 if PostgreSQL libraries are available])
-            found_postgresql="yes"
-            AC_MSG_RESULT(yes),
-            AC_MSG_RESULT(no))
-
-            CFLAGS="${_save_postgresql_cflags}"
-            LDFLAGS="${_save_postgresql_ldflags}"
-            LIBS="${_save_postgresql_libs}"
-            unset _save_postgresql_cflags
-            unset _save_postgresql_ldflags
-            unset _save_postgresql_libs
-
-            if test "x$found_postgresql" = "xyes"; then
-                POSTGRESQL_VERSION=`$PG_CONFIG --version | cut -d' ' -f2`
-            fi
-        fi
-    fi
+    AC_ARG_WITH([postgresql],
+	AS_HELP_STRING([--with-postgresql=@<:@ARG@:>@],
+	    [use PostgreSQL library @<:@default=yes@:>@, optionally specify path to pg_config]
+	),
+	[
+	AS_CASE([$withval],
+		[[[nN]][[oO]]],[want_postgresql="no"],
+		[[[yY]][[eE]][[sS]]],[want_postgresql="yes"],
+		[
+			want_postgresql="yes"
+			PG_CONFIG="$withval"
+		])
+	],
+	[want_postgresql="yes"]
+    )
 
-    dnl
-    dnl Check if required version of PostgreSQL is available
-    dnl
+    AC_ARG_VAR([POSTGRESQL_CPPFLAGS],[cpp flags for PostgreSQL overriding detected flags])
+    AC_ARG_VAR([POSTGRESQL_LIBFLAGS],[libs for PostgreSQL overriding detected flags])
+    AC_ARG_VAR([POSTGRESQL_LDFLAGS],[linker flags for PostgreSQL overriding detected flags])
+
+    # populate cache
+    AS_IF([test "X$POSTGRESQL_CPPFLAGS" != X],[ac_cv_POSTGRESQL_CPPFLAGS="$POSTGRESQL_CPPFLAGS"])
+    AS_IF([test "X$POSTGRESQL_LDFLAGS" != X],[ac_cv_POSTGRESQL_LDFLAGS="$POSTGRESQL_LDFLAGS"])
+    AS_IF([test "X$POSTGRESQL_LIBS" != X],[ac_cv_POSTGRESQL_LIBS="$POSTGRESQL_LIBS"])
 
     postgresql_version_req=ifelse([$1], [], [], [$1])
+    found_postgresql="no"
 
-    if test "x$found_postgresql" = "xyes" -a -n "$postgresql_version_req"; then
+    POSTGRESQL_VERSION=""
+
+    dnl
+    dnl Check PostgreSQL libraries (libpq)
+    dnl
+    AS_IF([test X"$want_postgresql" = "Xyes"],[
+      _AX_LIB_POSTGRESQL_PKG_CONFIG
 
-        dnl Decompose version string of installed PostgreSQL
-        dnl and calculate its number representation
-        postgresql_version_major=`expr $POSTGRESQL_VERSION : '\([[0-9]]*\)'`
-        postgresql_version_minor=`expr $POSTGRESQL_VERSION : '[[0-9]]*\.\([[0-9]]*\)'`
-        postgresql_version_micro=`expr $POSTGRESQL_VERSION : '[[0-9]]*\.[[0-9]]*\.\([[0-9]]*\)'`
-        if test "x$postgresql_version_minor" = "x"; then
-            postgresql_version_minor="0"
-        fi
-        if test "x$postgresql_version_micro" = "x"; then
-            postgresql_version_micro="0"
-        fi
-
-        postgresql_version_number=`expr $postgresql_version_major \* 1000000 \
-                                   \+ $postgresql_version_minor \* 1000 \
-                                   \+ $postgresql_version_micro`
-
-        if test -n "$postgresql_version_req"; then
-
-            AC_MSG_CHECKING([if PostgreSQL version is >= $postgresql_version_req])
-
-            dnl Decompose required version string of PostgreSQL
-            dnl and calculate its number representation
-            postgresql_version_req_major=`expr $postgresql_version_req : '\([[0-9]]*\)'`
-            postgresql_version_req_minor=`expr $postgresql_version_req : '[[0-9]]*\.\([[0-9]]*\)'`
-            postgresql_version_req_micro=`expr $postgresql_version_req : '[[0-9]]*\.[[0-9]]*\.\([[0-9]]*\)'`
-            if test "x$postgresql_version_req_micro" = "x"; then
-                postgresql_version_req_micro="0"
-            fi
-
-            postgresql_version_req_number=`expr $postgresql_version_req_major \* 1000000 \
-                                       \+ $postgresql_version_req_minor \* 1000 \
-                                       \+ $postgresql_version_req_micro`
-
-            postgresql_version_check=`expr $postgresql_version_number \>\= $postgresql_version_req_number`
-            if test "$postgresql_version_check" = "1"; then
-                AC_MSG_RESULT([yes])
-            else
-                AC_MSG_RESULT([no])
-            fi
 
-	fi
+      AS_IF([test X"$found_postgresql" = "Xno"],
+	    [_AX_LIB_POSTGRESQL_OLD])
 
-    fi
+      AS_IF([test X"$found_postgresql" = Xyes],[
+	  _AX_LIB_POSTGRESQL_OLD_CPPFLAGS="$CPPFLAGS"
+	  CPPFLAGS="$CPPFLAGS $POSTGRESQL_CPPFLAGS"
+	  _AX_LIB_POSTGRESQL_OLD_LDFLAGS="$LDFLAGS"
+	  LDFLAGS="$LDFLAGS $POSTGRESQL_LDFLAGS"
+	  _AX_LIB_POSTGRESQL_OLD_LIBS="$LIBS"
+	  LIBS="$LIBS $POSTGRESQL_LIBS"
+	  while true; do
+	    dnl try to compile
+	    AC_CHECK_HEADER([libpq-fe.h],[],[found_postgresql=no])
+	    AS_IF([test "X$found_postgresql" = "Xno"],[break])
+	    dnl try now to link
+	    AC_CACHE_CHECK([for the PostgreSQL library linking is working],[ac_cv_postgresql_found],
+	    [
+	      AC_LINK_IFELSE([
+		AC_LANG_PROGRAM(
+		  [
+		   #include <libpq-fe.h>
+		  ],
+		  [[
+		    char conninfo[]="dbname = postgres";
+		    PGconn     *conn;
+		    conn = PQconnectdb(conninfo);
+		  ]]
+		 )
+		],[ac_cv_postgresql_found=yes],
+		  [ac_cv_postgresql_found=no])
+	     ])
+	    found_postgresql="$ac_cv_postgresql_found"
+	    AS_IF([test "X$found_postgresql" = "Xno"],[break])
+	    break
+	done
+	CPPFLAGS="$_AX_LIB_POSTGRESQL_OLD_CPPFLAGS"
+	LDFLAGS="$_AX_LIB_POSTGRESQL_OLD_LDFLAGS"
+	LIBS="$_AX_LIB_POSTGRESQL_OLD_LIBS"
+	])
+
+
+      AS_IF([test "x$found_postgresql" = "xyes"],[
+		AC_DEFINE([HAVE_POSTGRESQL], [1],
+			  [Define to 1 if PostgreSQL libraries are available])])
+    ])
 
-    AC_SUBST([POSTGRESQL_CFLAGS])
+    AC_SUBST([POSTGRESQL_VERSION])
+    AC_SUBST([POSTGRESQL_CPPFLAGS])
     AC_SUBST([POSTGRESQL_LDFLAGS])
     AC_SUBST([POSTGRESQL_LIBS])
-    AC_SUBST([POSTGRESQL_VERSION])
+
+    AS_IF([test "x$found_postgresql" = "xyes"],
+     [ifelse([$2], , :, [$2])],
+     [ifelse([$3], , AS_IF([test X"$want_postgresql" = "Xyes"],[AC_MSG_ERROR([Library requirements (PostgreSQL) not met.])],[:]), [$3])])
+
 ])
